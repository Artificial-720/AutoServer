plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0-beta4'
}

dependencies {
    implementation project(":common")
    compileOnly "io.papermc.paper:paper-api:${project.paper_api_version}"
}

// Replaces the place holders in 'plugin.yml'
// '${version}' -> '1.0.0'
processResources {
    def props = [version    : version,
                 author     : author,
                 description: project.description,
                 minecraft_version: project.paper_minecraft_version
    ]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}


shadowJar {
    manifest {
        attributes('Main-Class': 'me.artificial.autoserver.common.BootListener')
    }

    archiveFileName.set("${rootProject.name}-${project.name}-${version}--shadow.jar")
}